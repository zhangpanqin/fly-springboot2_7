plugins {
    id 'java'
    id 'pmd'
    id 'checkstyle'
    id 'org.springframework.boot' apply false
    id 'io.spring.dependency-management' apply false
}

version = '0.0.1-SNAPSHOT'

allprojects {
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }
    group = "${global_group}"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'pmd'
    apply plugin: 'checkstyle'

    tasks.withType(Test).tap {
        configureEach {
            useJUnitPlatform()
        }
    }

    configurations {
        compileOnly.extendsFrom(annotationProcessor)
    }

    pmd {
        toolVersion = '6.0.0'
        rulesMinimumPriority = 5
        ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/pmd.xml')
        sourceSets = [project.sourceSets.main]
    }

    dependencies {
        pmd 'com.alibaba.p3c:p3c-pmd:2.1.1'
    }

    tasks.named('test') {
        mustRunAfter(tasks.pmdMain)
    }

    checkstyle {
        toolVersion = "9.3"
        config = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml')
        sourceSets = [project.sourceSets.main]
    }

    tasks.withType(Checkstyle).tap {
        configureEach {
            ignoreFailures = false
            reports {
                xml.required = true
                html.required = true
            }
        }
    }
}